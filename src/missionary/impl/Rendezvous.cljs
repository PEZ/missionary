(ns missionary.impl.Rendezvous
  (:import missionary.Cancelled))

(defn nop [])

(deftype Port [^:mutable readers
               ^:mutable writers]
  IFn
  (-invoke [_ t]
    (fn [s! f!]
      (if-some [[!] (seq readers)]
        (do (set! readers (disj readers !))
            (! t) (s! nil) nop)
        (let [! #(s! nil)]
          (set! writers (assoc writers ! t))
          #(when (contains? writers !)
             (set! writers (dissoc writers !))
             (f! (Cancelled. "Rendez-vous give cancelled.")))))))
  (-invoke [_ s! f!]
    (if-some [[[! t]] (seq writers)]
      (do (set! writers (dissoc writers !))
          (!) (s! t) nop)
      (let [! #(s! %)]
        (set! readers (conj readers !))
        #(when (contains? readers !)
           (set! readers (disj readers !))
           (f! (Cancelled. "Rendez-vous take cancelled.")))))))

(defn make [] (->Port #{} {}))
